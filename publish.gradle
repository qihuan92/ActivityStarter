apply plugin: 'maven-publish'
apply plugin: 'signing'

def isAndroidProject = project.hasProperty('android')

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (isAndroidProject) {
        from android.sourceSets.main.java.srcDirs
    } else {
        from sourceSets.main.allSource
    }
}

if (isAndroidProject) {
    task javadoc(type: Javadoc) {
        options.encoding = 'UTF-8'
        source = android.sourceSets.main.java.sourceFiles
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        android.libraryVariants.all { variant ->
            if (variant.name == 'release') {
                owner.classpath += variant.javaCompile.classpath
            }
        }
    }
} else {
    javadoc {
        options.encoding = 'UTF-8'
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name = "OSSRH"
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username = project.ossrhMavenUser
                    password = project.ossrhMavenPassword
                }
            }

            maven {
                name = "GitHubPackages"
                url = "https://maven.pkg.github.com/qihuan92/ActivityStarter"
                credentials {
                    username = project.githubMavenUser
                    password = project.githubMavenToken
                }
            }
        }

        publications {
            release(MavenPublication) {
                if (isAndroidProject) {
                    from components.release
                } else {
                    from components.java
                }
                artifact(sourcesJar)
                artifact(javadocJar)

                groupId project.publishGroupId
                artifactId project.artifactId
                version project.activityStarterVersionName

                pom {
                    name = project.artifactId
                    description = 'ActivityStarter'
                    url = 'https://github.com/qihuan92/ActivityStarter'
                    licenses {
                        license {
                            name = 'ActivityStarter License'
                            url = 'https://github.com/qihuan92/ActivityStarter/blob/master/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'qihuan'
                            name = 'qihuan'
                            email = 'qihuan92@126.com'
                        }
                    }

                    scm {
                        connection = 'scm:git:github.com/qihuan92/ActivityStarter.git'
                        developerConnection = 'scm:git:ssh://github.com/qihuan92/ActivityStarter.git'
                        url = 'https://github.com/qihuan92/ActivityStarter/tree/master'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            project.signingKeyId,
            project.signingKey,
            project.signingPassword,
    )
    sign publishing.publications
}