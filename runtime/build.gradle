plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

def runtimeVersionCode = 1
def runtimeVersionName = "1.0.0"

android {
    compileSdk 31

    defaultConfig {
        minSdk 21
        targetSdk 31
        versionCode runtimeVersionCode
        versionName runtimeVersionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    api project(path: ':annotation')
    implementation 'androidx.appcompat:appcompat:1.3.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name = "OSSRH"
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username = project.ossrhMavenUser
                    password = project.ossrhMavenPassword
                }
            }

            maven {
                name = "GitHubPackages"
                url = "https://maven.pkg.github.com/qihuan92/ActivityStarter"
                credentials {
                    username = project.githubMavenUser
                    password = project.githubMavenToken
                }
            }
        }

        publications {
            release(MavenPublication) {
                from components.release

                groupId "io.github.qihuan92.activitystarter"
                artifactId "activitystarter-runtime"
                version runtimeVersionName

                pom {
                    name = "activitystarter-runtime"
                    description = 'ActivityStarter'
                    url = 'https://github.com/qihuan92/ActivityStarter'
                    licenses {
                        license {
                            name = 'ActivityStarter License'
                            url = 'https://github.com/qihuan92/ActivityStarter/blob/master/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'qihuan'
                            name = 'qihuan'
                            email = 'qihuan92@126.com'
                        }
                    }

                    scm {
                        connection = 'scm:git:github.com/qihuan92/ActivityStarter.git'
                        developerConnection = 'scm:git:ssh://github.com/qihuan92/ActivityStarter.git'
                        url = 'https://github.com/qihuan92/ActivityStarter/tree/master'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            project.signingKeyId,
            project.signingKey,
            project.signingPassword,
    )
    sign publishing.publications
}